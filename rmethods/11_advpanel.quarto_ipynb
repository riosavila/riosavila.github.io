{
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "---\n",
        "title: \"Advanced Panel Data\"\n",
        "subtitle: \"More than one way to estimate a model\"\n",
        "author: Fernando Rios-Avila\n",
        "jupyter: nbstata\n",
        "format: \n",
        "  revealjs: \n",
        "    slide-number: true\n",
        "    width: 1600\n",
        "    height: 900\n",
        "    code-fold: true\n",
        "    code-overflow: wrap\n",
        "    echo: true\n",
        "    css: styles.css \n",
        "---\n",
        "\n",
        "\n",
        "## The old way (last class)\n",
        "\n",
        "- Last class we introduce the basic panel data model:\n",
        "\n",
        "$$y_{it} = \\alpha + \\beta x_{it} + \\alpha_i + \\delta_t + \\epsilon_{it}$$\n",
        "\n",
        "- This model could be estimated using First Differences approach. \n",
        "\n",
        "$$\\Delta y_{it} = \\beta \\Delta x_{it} + \\delta_t + \\Delta \\epsilon_{it}$$\n",
        "\n",
        "  - It elimitates the $\\alpha_i$, and constrains how $\\delta_t$ is estimated.\n",
        "  - It allows you to related how changes in $x_{it}$ are related to changes in $y_{it}$.\n",
        "    - Thus Fixed variables across time cannot be identified.\n",
        "\n",
        "- It requires strong assumptions of strict exogeneity and no serial correlation.\n",
        "\n",
        "## The new way (This class)\n",
        "\n",
        "- Today we are going to describe the use of three other methods:\n",
        "\n",
        "  - Fixed Effects (FE)\n",
        "  - Random Effects (RE)\n",
        "  - Correlated Random effects (CRE) $\\simeq$ FE+RE \n",
        "  \n",
        "- This method require their own methods, but could be used in more flexible scenarios.\n",
        "\n",
        "- What do we mean Fixed effects? Random Effects? Correlated Random Effects?\n",
        "  - All this will be estimation methods that relate to the same model. \n",
        "  - However, in all cases, we assume the unobserved are fixed factors across time. We simply identify them differently.\n",
        "\n",
        "## Fixed Effects Estimation\n",
        "\n",
        "Lets consider the following model:\n",
        "$$y_{it} = \\beta_1 x_{it} + \\beta_2 z_{it} + \\alpha_i + \\epsilon_{it}$$\n",
        "\n",
        "which doesnt include a time-fixed effect, nor time-invariante factors.\n",
        "\n",
        "- This could be estiamted simply adding dummies for each individual in the data set. (too many dummies). Instead consider the following\n",
        "\n",
        "- Now, for each person, lets estimate the average characteristics $\\bar w = \\frac{1}{T} \\sum_{t=1}^T w_{it}$. We could apply this to the model above an dobtain:\n",
        "\n",
        "$$\\bar y_{i} = \\beta_1 \\bar x_{i} + \\beta_2 \\bar z_{i} + \\alpha_i + \\bar \\epsilon_{i}$$\n",
        "\n",
        "- This no longer change across time. \n",
        "  \n",
        "- It is a model interesting on itself. It captures Between Effects. \n",
        "\n",
        "## \n",
        "\n",
        "- Now, lets substract this from the original model:\n",
        "\n",
        "$$y_{it}-\\bar y_{i} = \\beta_1 (x_{it}- \\bar x_{i}) + \\beta_2 (z_{it}- \\bar z_{i}) + e_{it} - \\bar \\epsilon_{i}$$\n",
        "$$\\tilde y_{it} = \\beta_1 \\tilde  x_{it} + \\beta_2 \\tilde  z_{it} + \\tilde \\epsilon_{it}$$\n",
        "\n",
        "- What we have just done is apply the within transformation. The model above now captures the relationship between $X's$ and $Y's$ using only changes within each individual.\n",
        "  - This \"ignores\" variation across individuals.\n",
        "- This within transformation eliminates all time-invariant factors, including $\\alpha_i$.\n",
        "\n",
        "Also of interest:\n",
        "  * This model could now be estimated using OLS\n",
        "  * Its an application of the FWL theorem. (we partial out the time-invariant factors)\n",
        "  * If done by OLS, you need to correct the Degrees of freedom. (NT-**N**-k)\n",
        "\n",
        "## Expanding the model: Time fixed effects\n",
        "\n",
        "- Now, lets consider the following model:\n",
        "\n",
        "$$y_{it} = \\beta_1 x_{it} + \\beta_2 z_{it} + \\alpha_i + \\delta_t + \\epsilon_{it}$$\n",
        "\n",
        "- We could apply the same transformation as before, but now we need to consider the $\\delta_t$.\n",
        "  - Typically, the number of time periods is small, and we could control for them using dummies. (need to be explicit about it)\n",
        "  - Altenativelly, One may need to use a Douple Demeaning approach. \n",
        "\n",
        "$$\\tilde y_{it} = y_{it} - \\bar y_i - \\bar y_t + \\bar y$$\n",
        "\n",
        "where $\\bar y_t$ is the average across individuals, and $\\bar y$ is the overall average of $y_{it}$.\n",
        "\n",
        "- This will work as intended if the panel is balanced.  \n",
        "\n",
        "## When Panel is not balanced:\n",
        "\n",
        "- If panel is not balanced, you need to `demean` data using interative methods. \n",
        "  \n",
        "- Lets assume that $\\bar y=0$. We would need to demean the data many times as follows:\n",
        "\n",
        "$$ \\overline{ty}_{it} = y_{it} - \\bar y_i - \\bar y_t $$\n",
        "$$ \\overline{tty}_{it} = \\overline{ty}_{it} - \\overline{ty}_i - \\overline{ty}_t $$\n",
        "$$ \\overline{ttty}_{it} = \\overline{tty}_{it} - \\overline{tty}_i - \\overline{tty}_t $$\n",
        "\n",
        "So on and so forth, until there is no more variation in the transformed data.\n",
        "\n",
        "- i.e. $\\overline{t \\dots ty}_i = \\overline{t\\dots t y}_t=0$\n",
        "\n",
        "**NOTE**: There are more efficient ways to do this.\n",
        "\n",
        "## FE vs FD: Balance Panel\n",
        "\n",
        "- FE and FD both aim to estimate the model by \"eliminating\" individual effects $\\alpha_i$.\n",
        "- With $T=2$, both will give you the same results.\n",
        "- With $T\\geq 3$, you may need to choose based on assumptions on the error\n",
        "  - if $e_{it}$ is serially uncorrelated, then FE is more efficient. Otherwise, FD may be better (if correlation is strong)\n",
        "- Otherwise, typical suggestionis to try both, and evaluate the results.\n",
        "- In general, there are few arguments to choose between FD and FE. \n",
        "  - Empirically, People use FE, because its the default in most software.\n",
        "\n",
        "## FE vs FD: Unbalanced Panel\n",
        "\n",
        "- Unbalance panel data occures when different units are observed over different time periods.\n",
        "  - Some periods may or may not overlap, some may skip periods, etc\n",
        "  - It may be more important understanding why one observes this kind of missing data problem. \n",
        "- If this is the case FD may be more difficult to use, because it requires data with regular time gaps. (Observations with missing data may be dropped)\n",
        "- With FE, you make most use of available data. Only those with \"singletons\" (units observed only once) are dropped.\n",
        "\n",
        "## Random Effects Models \n",
        "\n",
        "- Even if not done by hand, FE estimation is very computationally intensive and inneficient, because it requires estimating a large set of coefficients for indivuals. \n",
        "  - This, however, its important if we believe that $\\alpha_i$ are correlated with $x_{it}$.\n",
        "\n",
        "- If $\\alpha_i$ were uncorrelated with $x_{it}$, then we could use a more efficient Approach: Random Effects model.\n",
        "  - $Corr(\\alpha_i, x_{it})=0$ can be a very hard assumption to make.\n",
        "\n",
        "- If this is the case, we could estimate the model using OLS or Pool-OLS. Both would be consistent. \n",
        "  - However, the standard errors would be biased, because of the correlation across errors. \n",
        "\n",
        "$$Corr(e_{it}+a_i,e{is}+a_i)=\\frac{\\sigma^2_a}{\\sigma^2_a+\\sigma^2_e}$$ \n",
        "\n",
        "## Random Effects Models: SE estimation\n",
        "\n",
        "- There are two ways to estimate the standard errors in a Random Effects model:\n",
        "  - One could be to apply \"clustered-robust\" standard errors, using the individual as the cluster.\n",
        "    - Its a genereric solution to Clustering...Specially appropriate if we do not know how Clustering happens. (but we know\n",
        "  - The second One is apply `GLS`. Since we know the \"theoretical\" correlation across errors, we could use this to transform the data, and estimate SE.\n",
        "\n",
        "## \n",
        "\n",
        "First, define: \n",
        "\n",
        "$$\\theta = 1- \\left[ \\frac{\\sigma^2_e}{\\sigma^2_e+T \\sigma^2_a}\\right]^{1/2}$$\n",
        "\n",
        "- All variables in the model (inclulding the constant) should be transformed using a quasi-differentiation as follows:\n",
        "\n",
        "$$\\tilde w_{it} = w_{it} - \\theta \\bar w_i$$\n",
        "\n",
        "- This transformation will eliminate the correlation across errors, and allow us to estimate the model using OLS.\n",
        "$$y_{it}-\\theta \\bar y_i = \\beta_0 (1-\\theta) + b_1 (x_{it} - \\theta \\bar x_i) + b_2 (z_{it} - \\theta \\bar x_i) + v_{it} - \\theta \\bar v_i$$\n",
        "  \n",
        "## \n",
        "### Last pieces of the puzzle:\n",
        "\n",
        "1. Estimate the main model using pool OLS. $y_{it}=\\beta_0 + \\beta_1 x_{it} + v_{it}$\n",
        "\n",
        "2. Estimate $\\sigma^2_a$ as: $\\hat \\sigma^2_a = \\frac{\\sum_{i=1}^N \\sum_{t=1}^{T-1}\\sum_{s=t+1}^{T} \\hat v_{it} \\hat v_{is}}{NT(T-1)/2 - (k+1)}$\n",
        "\n",
        "3. Estimate $\\sigma^2_e$ as: $\\hat \\sigma_e^2 = \\hat\\sigma^2_v - \\hat \\sigma^2_a$\n",
        "\n",
        "- Biggest Advantage of RE model is that you can now obtain effects for Time-invariant variables. \n",
        "\n",
        "- It is also more efficient, because you do not need to estimate individual effects, just capture the distribution of $\\alpha_i$.\n",
        "\n",
        "## Example {.scrollable}\n",
        "\n",
        "In `Stata`, you could estimate the panel models using the `xtreg` command. \n",
        "\n",
        "This command has options for Fixed Effects, Between Effects and Random Effects. \n"
      ],
      "id": "472d241e"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "frause wagepan, clear\n",
        "** Good idea to Set the data as panel data\n",
        "xtset nr year"
      ],
      "id": "86dac410",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "** Pool OLS\n",
        "qui: reg lwage educ black hisp exper expersq married union,\n",
        "est sto m1\n",
        "** pool OLS with Clustered SE\n",
        "qui: reg lwage educ black hisp exper expersq married union, cluster(nr)\n",
        "est sto m2\n",
        "** Random Effects: Default\n",
        "qui:xtreg lwage educ black hisp exper expersq married union, re\n",
        "est sto m3\n",
        "** Fixed Effects\n",
        "qui:xtreg lwage educ black hisp exper expersq married union, fe\n",
        "est sto m4\n",
        "esttab m1 m2 m3 m4, se b(4) noomit nonumber mtitle(OLS OLS_CL RE FE)"
      ],
      "id": "a6c8e1bb",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "## Pool OLS vs RE vs FE\n",
        "\n",
        "- We now know how to analyze panel data using three Stretegies: Pool OLS, Fixed Effects and,  Random Effects.\n",
        "\n",
        "  1. FE is usually prefered to RE, because is more consistent by relaxing the assumption of no correlation between $a_i$ and $x_{it}$ (explicit control). Its less efficient.\n",
        "  2. RE may be prefer to FE if the correlation between $a_i$ and $x_{it}$ is small. Its more efficient, and allows to estimate effects for **time-invariant variables**.\n",
        "  3. RE and POLS will be consistent under the same assumptions. However, RE will remove some of the serial correlation, and may have less bias than OLS (even if $a_i$ and $x_{it})$ are correlated.\n",
        "\n",
        "- Choosing between RE and POLs is rarely considered. (RE would be the default in most cases)\n",
        "- However, Choosing between FE vs RE is common: Hausman Test\n",
        "\n",
        "## Hausman Test {.scrollable}\n",
        "\n",
        "- Hausman test is used to determine which model to use between two estimators.\n",
        "  1. You assume FE is consistent (but not efficient).\n",
        "  2. You estimate the model using RE. If RE estimates are close to FE, then RE is consistent and efficient (preferred)\n",
        "  3. Othewise, we suspect RE are inconsistent, and we use FE.\n",
        "- For most applied work, however, FE is generally prefered to RE\n"
      ],
      "id": "8ad1f0e2"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "** Hausman Test\n",
        "*** Consistent model FE\n",
        "qui:xtreg lwage educ black hisp exper expersq married union, fe\n",
        "est sto fe\n",
        "*** Efficient under H0\n",
        "qui:xtreg lwage educ black hisp exper expersq married union, re\n",
        "est sto re\n",
        "hausman fe re"
      ],
      "id": "c6b8ea0a",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "## Correlated Random Effects\n",
        "\n",
        "- The CRE model is an alternative approach that combines some of the features of RE and FE estimators.\n",
        "$$y_{it} = \\beta_1 x_{it} + \\beta_2 z_{i} + \\alpha_i + \\epsilon_{it}$$\n",
        "\n",
        "- One way to look at the \"individual\" fixed effect is to model it as a function of fixed effects:\n",
        "\n",
        "$$\\alpha_i = \\alpha + \\gamma_1 \\bar x_{i} + \\gamma_2 z_{i} + r_i$$\n",
        "\n",
        "- In this case, we assume the fixed unobserved effect $\\alpha_i$ could be written as a function of avg observed characteristics, and fixed factors.\n",
        "\n",
        "- And we assume that $r_i$ would be uncorrelated with $x_{it}$ , $\\bar x_i$ and $z_{i}$.\n",
        "\n",
        "##\n",
        "\n",
        "- If we combine this with the main regression we have:\n",
        "\n",
        "$$y_{it} = \\beta_1 x_{it} + \\beta_2 z_{i} + \\alpha + \\gamma_1 \\bar x_{i} + \\gamma_2 z_{i} + r_i + \\epsilon_{it}$$\n",
        "$$y_{it} = \\alpha + \\beta_1 x_{it} + \\beta_2 z_{i} + \\gamma_1 \\bar x_{i} + \\underbrace{\\nu_{it}}_{ r_i + \\epsilon_{it}}$$\n",
        "\n",
        "- Which we could now estimate using Pool OLS or RE. There is no more need to worry about correlation between $r_i$ and $x_{it}$.\n",
        "\n",
        "- Differences and Advantages:\n",
        "  - We must include individual level average characteristics.\n",
        "  - We can now estimate effects for time-invariant variables.\n",
        "  - We can test for FE vs RE models.\n",
        "\n",
        "## Correlated Random Effects: FE vs RE\n",
        "\n",
        "- CRE estimates for Time varying variables are identical to FE. \n",
        "  $$\\hat \\beta_{cre}=\\hat \\beta_{fe}$$\n",
        "\n",
        "- CRE estimates shows clearly why RE are more efficient (RE imposes $\\gamma=0$)\n",
        "\n",
        "- Thus, we can test for FE vs RE using the following test:\n",
        "\n",
        "$$H_0: \\gamma = 0 \\text{ or } RE $$\n",
        "$$H_a: \\gamma \\neq 0 \\text{ or } FE $$\n",
        "\n",
        "## Example {.scrollable}\n"
      ],
      "id": "a17f700d"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "foreach i of varlist exper expersq married union {\n",
        "  bysort nr: egen mn_`i'=mean(`i')\n",
        "}\n",
        "xtreg lwage educ black hisp exper expersq married union mn_*, re\n",
        "est sto m2\n",
        "** FE vs RE\n",
        "test mn_exper mn_expersq mn_married mn_union"
      ],
      "id": "77d8b23f",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "Comparing across models:\n"
      ],
      "id": "f29d8a3e"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "qui:xtreg lwage educ black hisp exper expersq married union, fe\n",
        "est sto m1\n",
        "qui:xtreg lwage educ black hisp exper expersq married union, re\n",
        "est sto m3\n",
        "esttab m1 m2 m3, se b(4) noomit nonumber mtitle(FE CRE RE)"
      ],
      "id": "c9925e2b",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "## CRE Implementation {.scrollable}\n",
        "\n",
        "- As shown above, in `Stata`, you could estimate the CRE panel models using the `xtreg` with RE option. \n",
        "  - You just need to be careful when estimating the averages of all variables in the model. \n",
        "  - This is particularly relevant for unbalance panel data.\n",
        "  - In those cases, you could use `cre` (from fra install)  \n",
        "\n",
        "- You could also extend this to using Multiple fixed effects (time and individual), but some equivalences are lost.\n"
      ],
      "id": "ab9c60f9"
    },
    {
      "cell_type": "code",
      "metadata": {},
      "source": [
        "fra install cre\n",
        "cre , abs(nr): xtreg lwage educ black hisp exper expersq married union, re "
      ],
      "id": "e425165b",
      "execution_count": null,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {},
      "source": [
        "## Final words\n",
        "\n",
        "- All estimation methodologies presented here could also be used in other contexts.\n",
        "- Examples:\n",
        "  - Geronimus and Korenman (1992): Analysis of data siblings outcome accounting for family fixed effects.\n",
        "  - Ashenfelder and Kruger (1994): Return to education using Twins Data.\n",
        "\n",
        "- One may also use the principles of Panel data with linked data in high dimensional data sets. \n",
        "  - Education data and School FE\n",
        "  - Health data and Hospital FE\n",
        "  - Wages and Firm FE\n",
        "  - etc.\n",
        "\n",
        "- In this cases, one may need to also considering explicit clustering in addition to \"fixed effects\".\n",
        "\n",
        "# That's all folks!\n",
        "Next week: Switching gears to Time Series Analysis\n",
        "- When the present, the past and the future matter"
      ],
      "id": "8bec603d"
    }
  ],
  "metadata": {
    "kernelspec": {
      "name": "nbstata",
      "language": "stata",
      "display_name": "Stata (nbstata)",
      "path": "C:\\Users\\Fernando\\AppData\\Roaming\\jupyter\\kernels\\nbstata"
    }
  },
  "nbformat": 4,
  "nbformat_minor": 5
}